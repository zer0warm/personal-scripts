#! /bin/zsh

# Quickly note something on the fly

# Exit on error
set -e

# Find editor
editor=${VISUAL:-${EDITOR:-nvim}}
if ! which $editor >/dev/null 2>&1; then exit 1; fi

# Default note location
vault=$HOME/writings/justwrite
[[ ! -d $vault ]] && mkdir -p $vault

# Non-editing commands
case "$1" in
  # Browse notes, use fzf
  browse)
    oldpwd=$(pwd)
    cd $vault
    notefile=$(ls -t *.txt | while read note; do
      print "$note -- $(head -n 1 $note | cut -c 3-) ($(tail -n 1 $note))"
    done \
      | fzf --layout=reverse \
            --border=bold \
            --border-label='scribbled notes' \
            --header="Tags:
$(tail -qn 1 *.txt | tr ' ' "\n" | sort | uniq | paste -sd' ' - | fmt -sw $(( $(tput cols) - 10 )))" \
            --header-first \
            --cycle \
            --accept-nth=1 \
            --no-sort)
    # Exit early if nothing was selected
    if [[ -z "$notefile" ]]; then exit 1; fi
    $editor $notefile
    cd $oldpwd
    exit
    ;;
esac

tag=
case "$1" in
  ask)
    tag=question
    shift
    ;;
  think)
    tag=thought
    shift
    ;;
  *)
    tag=uncategorized
    ;;
esac

notefile=${vault}/$(date +"%Y%m%d%H%M%S").txt

if [[ -z "$*" ]]; then
  $editor $notefile
else
  content=$*
  [[ $tag = question ]] && content=$content'?'
  # Title is capitalized content
  title=$(python3 -c 'print("'$content'".capitalize())')
  cat > $notefile << EOF
= $title

$content

#$tag
EOF
fi

[[ -f $notefile ]] && print "Wrote $notefile" || exit 0
